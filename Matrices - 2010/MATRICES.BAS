10 SCREEN 0:WIDTH 80:KEY OFF'DEFSNG A-Z
20 DIM A(81):DIM AS(81):DIM AT(81):DIM S(73):DIM W(9):DIM WW(9)
30 GOSUB 920:LOCATE 3,3:PRINT "Q: Operations":LOCATE 3,4:PRINT "W: Equations"
40 GOSUB 70
50 Q$=INKEY$:IF Q$="Q" OR Q$="q" THEN 90
60 IF Q$="W" OR Q$="w" THEN 1200 ELSE 50
70 IF INKEY$<>"" THEN 70
80 RETURN
90 GOSUB 920:GOSUB 930:GOSUB 940:GOSUB 950
100 IF NC<>NF OR NC+NF=2 THEN UX=9 ELSE UX=14
110 RESTORE 120:FOR M=1 TO UX:READ Z,X,Z$:LOCATE X,Z:PRINT Z$:NEXT
120 DATA 15,2,"A: Sum",16,2,"S: Subtraction",17,2,"D: Product",18,2,"F: Scaler",19,2,"G: Transpose",20,2,"H: Row echelon",21,2,"J: Minor",22,2,"K: Cofactor",15,17,"Z: Correct",17,17,"C: Determinant",18,17,"V: Self-adjoint",19,17,"B: Adjugate"
130 DATA 20,17,"N: Inverse",21,17,"M: Exponent"
140 GOSUB 70
150 Q$=INKEY$:IF Q$="A" OR Q$="a" THEN 330
160 IF Q$="S" OR Q$="s" THEN 370
170 IF Q$="D" OR Q$="d" THEN 410
180 IF Q$="F" OR Q$="f" THEN 460
190 IF Q$="G" OR Q$="g" THEN 470
200 IF Q$="H" OR Q$="h" THEN 520
210 IF Q$="J" OR Q$="j" THEN 640
220 IF Q$="K" OR Q$="k" THEN 740
230 IF Q$="Z" OR Q$="z" THEN GOSUB 750:GOTO 360
240 IF UX=14 THEN 260
250 IF Q$=CHR$(27) THEN RUN ELSE 150
260 IF Q$="C" OR Q$="c" THEN 780
270 IF Q$="V" OR Q$="v" THEN 790
280 IF Q$="B" OR Q$="b" THEN 840
290 IF Q$="N" OR Q$="n" THEN 850
300 IF Q$="M" OR Q$="m" THEN 860
310 GOTO 250
320 GOTO 150
330 GOSUB 910:GOSUB 920:GOSUB 1000
340 GOSUB 950:GOSUB 1010:GOSUB 920
350 FOR M=1 TO NC*NF:A(M)=A(M)+AS(M):NEXT
360 GOSUB 980:GOSUB 1000:GOTO 100
370 GOSUB 910:GOSUB 920:GOSUB 1000
380 GOSUB 950:GOSUB 1010:GOSUB 920
390 FOR M=1 TO NC*NF:A(M)=AS(M)-A(M):NEXT
400 GOTO 360
410 GOSUB 920:PRINT"Rows="NC:J=NF:GOSUB 910:QP=1:GOSUB 1190
420 NF=NC:GOSUB 940:GOSUB 950:GOSUB 1010:GOSUB 920:GOSUB 430:GOTO 360
430 FOR M=1 TO NC*J:FOR B=0 TO NF-1
440 AT(M)=AT(M)+AS(1+INT((M-1)/NC)*NF+B)*A((M-1) MOD NC+1+B*NC):NEXT B,M
450 FOR M=1 TO NC*J:A(M)=AT(M):NEXT:NF=J:RETURN
460 LOCATE 1,4:INPUT "Scalar to multiply";J:GOSUB 920:FOR M=1 TO NC*NF:A(M)=A(M)*J:NEXT:GOTO 360
470 GOSUB 920:GOSUB 910:GOSUB 480:GOSUB 980:GOTO 510
480 SWAP NF,NC:FOR B=0 TO NF-1:FOR M=1 TO NC
490 A(M+B*NC)=AS((B+1)+(M-1)*NF):NEXT M,B
500 RETURN
510 GOSUB 1000:GOTO 100
520 GOSUB 530:GOSUB 920:GOTO 360
530 X=NC*NF-NC+1:K=1:HH=1:CM=1:ERASE S:DIM S(73)
540 IF NC>=NF-1 THEN AW=NF-2 ELSE AW=NC
550 FOR P=0 TO AW:FOR T=0 TO NF-2-P
560 IF A(X-T*NC+P)=0 THEN 620
570 IF A(X-T*NC+P-NC)=0 THEN FOR M=0 TO NC-1:SWAP A(X-T*NC+M),A(X-T*NC+M-NC):NEXT:HH=HH+1:GOTO 620
580 S(K)=A(X-T*NC+P):S(K+1)=A(X-T*NC+P-NC)
590 FOR Q=0 TO NC-1
600 A(X+Q-T*NC)=A(X+Q-T*NC)*S(K+1)-A(X+Q-NC-T*NC)*S(K)
610 NEXT Q
620 K=K+2
630 NEXT T,P:RETURN
640 IF NC=1 OR NF=1 THEN LOCATE 1,4:PRINT"Cannot be done!";SPACE$(8):FOR M=1 TO 9999:NEXT:LOCATE 1,4:PRINT SPACE$(15):GOTO 140
650 E=0:GOSUB 1060:GOSUB 910:GOSUB 660:GOSUB 700
660 FOR M=1 TO NF*NC
670 IF 1+INT((M-1)/NC)=F OR (M-1) MOD NC+1=L THEN 690
680 E=E+1:A(E)=AS(M)
690 NEXT:RETURN
700 GOSUB 920:NF=NF-1:NC=NC-1:GOSUB 1000:GOSUB 980
710 LOCATE 1,4:PRINT"[ESC] original, [ENTER] accept":GOSUB 70
720 Q$=INKEY$:IF Q$=CHR$(13) THEN LOCATE 1,4:PRINT SPACE$(32):GOTO 100
730 IF Q$=CHR$(27) THEN NF=NF+1:NC=NC+1:FOR M=1 TO NF*NC:A(M)=AS(M):NEXT:GOSUB 920:GOSUB 1000:GOSUB 980:GOSUB 110:ELSE 720
740 GOSUB 1060:LOCATE 1,4:PRINT"Cofactor ="A(L+(F-1)*NC):GOTO 140
750 GOSUB 1060:LOCATE 1,4:PRINT"Change "A(L+(F-1)*NC)
760 LOCATE 11+LEN(STR$(A(L+(F-1)*NC))),4:INPUT"to ";I
770 A(L+(F-1)*NC)=I:I=0:GOSUB 920:RETURN
780 GOSUB 1150:GOSUB 530:GOSUB 1110:LOCATE 1,4:PRINT"Determinant="CM:GOSUB 1170:GOTO 140
790 GOSUB 800:GOTO 830
800 LOCATE 1,4:PRINT"Wait...";SPACE$(27):GOSUB 910
810 FOR F=1 TO NC:FOR L=1 TO NC:E=0:GOSUB 660:NF=NF-1:NC=NC-1:GOSUB 530:GOSUB 1110:NF=NF+1:NC=NC+1:IO=IO+1:AT(IO)=CM*(-1)^(L+F)
820 NEXT L,F:IO=0:GOSUB 1180:RETURN
830 GOSUB 920:GOSUB 980:GOSUB 1000:GOTO 110
840 GOSUB 800:GOSUB 910:GOSUB 480:GOTO 830
850 GOSUB 1150:GOSUB 530:GOSUB 1110:GOSUB 1170:IF CM=0 THEN 900 ELSE LE=CM:GOSUB 800:GOSUB 910:GOSUB 480:FOR M=1 TO NC^2:A(M)=A(M)/LE:NEXT:GOTO 830
860 J=NC:FP=0
870 LOCATE 1,4:PRINT SPACE$(30)
880 LOCATE 1,4:INPUT"Exponent (1-9)";FP:IF FP>9 OR FP<1 THEN 870
890 GOSUB 1150:FOR ZJ=1 TO FP-1:GOSUB 430:NEXT:GOSUB 920:GOSUB 1000:GOSUB 980:GOTO 110
900 LOCATE 1,4:PRINT"Cannot be inverted!":GOTO 140
910 FOR M=1 TO NC*NF:AS(M)=A(M):A(M)=0:NEXT:RETURN
920 CLS:PRINT "- MATRICES -":RETURN
930 LOCATE 1,2:INPUT"Rows";NF:IF NF>9 OR NF<1 THEN 930 ELSE RETURN
940 LOCATE 1,3:INPUT"Columns";NC:IF NC>9 OR NC<1 THEN 940 ELSE RETURN
950 FOR B=0 TO NF-1:FOR M=1 TO NC
960 LOCATE 1,4:PRINT SPACE$(15)
970 LOCATE 1,4:INPUT A(M+B*NC):LOCATE M*9-8,B+6:PRINT INT(A(M+B*NC)*10000+.5)/10000:NEXT M,B:RETURN
980 FOR B=0 TO NF-1:FOR M=1 TO NC
990 LOCATE M*9-8,B+6:PRINT INT(A(M+B*NC)*10000+.5)/10000:NEXT M,B:RETURN
1000 LOCATE 1,2:PRINT"Rows="NF:LOCATE 1,3:PRINT"Columns="NC:RETURN
1010 LOCATE 1,4:PRINT"[Enter] Result, [Z] Correct, [ESC] Original"
1020 GOSUB 70
1030 Q$=INKEY$:IF Q$=CHR$(13) THEN RETURN
1040 IF Q$="Z" OR Q$="z" THEN GOSUB 750:GOSUB 920:GOSUB 980:GOSUB 1000:GOTO 1010
1050 IF Q$=CHR$(27) THEN GOSUB 1160:GOSUB 920:GOTO 360 ELSE 1030
1060 LOCATE 1,4:PRINT SPACE$(49)
1070 LOCATE 1,4:INPUT"Row  ";F:IF F>NF OR F<1 THEN 1060
1080 LOCATE 1,4:PRINT SPACE$(20)
1090 LOCATE 1,4:INPUT"Column";L:IF L>NC OR L<1 THEN 1080
1100 RETURN
1110 FOR V=0 TO NC-1:CM=CM*A(1+V*NC+V):NEXT
1120 FOR V=2 TO K-1 STEP 2:IF S(V)=0 THEN 1140
1130 CM=CM/S(V)
1140 NEXT:CM=CM*(-1)^(HH+1):RETURN
1150 FOR M=1 TO NC^2:AS(M)=A(M):NEXT:RETURN
1160 IF QP=1 THEN QP=0:NC=NF:NF=J
1170 FOR M=1 TO NC*NF:A(M)=AS(M):NEXT:RETURN
1180 FOR AD=1 TO NC^2:A(AD)=AT(AD):NEXT:RETURN
1190 FOR AD=1 TO NC^2:AT(AD)=0:NEXT:RETURN
1200 GOSUB 920
1210 LOCATE 1,2:INPUT"Number of equations (2-7)";NC
1220 IF NC<2 OR NC>7 THEN 1210
1230 FOR B=1 TO NC:LOCATE 1,3:PRINT"Equation  number "B:FOR M=1 TO NC
1240 LOCATE 1,4:PRINT"x"RIGHT$(STR$(M),1);SPACE$(12):LOCATE 4,4:INPUT A(M+(B-1)*NC):IF A(M+(B-1)*NC)>=1000 OR A(M+(B-1)*NC)<=-1000 THEN 1240
1250 LOCATE 9*M-LEN(STR$(ABS(INT(ABS(A(M+(B-1)*NC))*100+.5)/100)))-1,6+B:PRINT INT(ABS(A(M+(B-1)*NC))*100+.5)/100
1260 LOCATE 9*M-1,6+B:PRINT"x"RIGHT$(STR$(M),1):RO=8
1270 IF M=1 THEN IF A(M+(B-1)*NC)<0 THEN RO=LEN(STR$(INT(A(M+(B-1)*NC)*100+.5)/100))+1 ELSE 1300
1280 IF A(M+(B-1)*NC)>=0 THEN ER=43 ELSE ER=45
1290 LOCATE 9*M-RO,6+B:PRINT CHR$(ER)
1300 NEXT
1310 LOCATE 9*M-8,6+B:PRINT"="
1320 LOCATE 1,4:PRINT"a"RIGHT$(STR$(B),1);SPACE$(12):LOCATE 4,4:INPUT W(B):IF W(B)>=1000 OR W(B)<=-1000 THEN 1320
1330 LOCATE 9*M-7,6+B:PRINT INT(W(B)*100+.5)/100:NEXT
1340 RESTORE 1350:FOR K=1 TO 3:READ Z$:LOCATE 2,16+K:PRINT Z$:NEXT
1350 DATA "E: Solve All","R: Correct Coefficient","T: Correct Result"
1360 GOSUB 70
1370 Q$=INKEY$:IF Q$="E" OR Q$="e" THEN 1410
1380 IF Q$="R" OR Q$="r" THEN 1520
1390 IF Q$="T" OR Q$="t" THEN 1670
1400 IF Q$=CHR$(27) THEN RUN ELSE 1370
1410 NF=NC:GOSUB 1510:LOCATE 1,17:PRINT"Wait...":GOSUB 1150:GOSUB 530:GOSUB 1110:GOSUB 1170:SP=CM
1420 IF SP=0 THEN LOCATE 1,17:PRINT"There's no solution!":GOTO 1480
1430 FOR IM=1 TO NC:FOR KH=1 TO NC:A(IM+(KH-1)*NC)=W(KH):NEXT
1440 GOSUB 530:GOSUB 1110:GOSUB 1170:WW(IM)=CM:NEXT
1450 LOCATE 1,17:PRINT SPACE$(10)
1460 FOR M=1 TO NC:SG=SG+1:IF M=5 THEN SG=1:SH=1
1470 LOCATE 18*SG-17,17+SH:PRINT"x"RIGHT$(STR$(M),1)"="INT(WW(M)/SP*10000+.5)/10000:NEXT
1480 SH=0:SG=0:LOCATE 1,20:PRINT"Press [ENTER]"
1490 GOSUB 70
1500 IF INKEY$=CHR$(13) THEN GOSUB 1510:GOTO 1340 ELSE 1500
1510 FOR M=1 TO 4:LOCATE 1,16+M:PRINT SPACE$(77):NEXT:RETURN
1520 GOSUB 1660:LOCATE 1,4:INPUT "Variable x";NV
1530 IF NV>NC OR NV<1 THEN 1520
1540 GOSUB 1660:LOCATE 1,4:INPUT"Equation number";NE
1550 IF NE>NC OR NE<1 THEN 1540
1560 GOSUB 1660:LOCATE 1,4:PRINT"Change "A(NV+(NE-1)*NC):LOCATE 11+LEN(STR$(A(NV+(NE-1)*NC))),4:INPUT "to";VN
1570 IF VN>=1000 OR VN<=-1000 THEN 1560
1580 A(NV+(NE-1)*NC)=VN:RO=8
1590 LOCATE 1+(NV-1)*9,6+NE:PRINT SPACE$(6)
1600 LOCATE 9*NV-LEN(STR$(ABS(INT(ABS(A(NV=(NE-1)*NC))*100+.5)/100)))-1,6+NE:PRINT INT(ABS(A(NV+(NE-1)*NC))*100+.5)/100
1610 IF NV=1 THEN IF A(NV+(NE-1)*NC)<0 THEN RO=LEN(STR$(INT(A(NV+(N-1)*NC)*100+.5)/100))+1 ELSE 1640
1620 IF A(NV+(NE-1)*NC)>=0 THEN ER=43 ELSE ER=45
1630 LOCATE 9*NV-RO,6+NE:PRINT CHR$(ER)
1640 LOCATE 9*NV-1,6+NE:PRINT"x"
1650 GOSUB 1660:GOTO 1340
1660 LOCATE 1,4:PRINT SPACE$(35):RETURN
1670 GOSUB 1660:LOCATE 1,4:INPUT"Result of equation number";RN
1680 IF RN>NC OR RN<1 THEN 1670
1690 GOSUB 1660:LOCATE 1,4:PRINT"Change "W(RN):LOCATE 11+LEN(STR$(W(RN))),4:INPUT "to ";RV
1700 IF RV>=1000 OR RV<=-1000 THEN 1690
1710 W(RN)=RV:LOCATE 9*NC+2,6+RN:PRINT SPACE$(7)
1720 LOCATE 9*NC+2,6+RN:PRINT INT(W(RN)*100+.5)/100:GOTO 1650
